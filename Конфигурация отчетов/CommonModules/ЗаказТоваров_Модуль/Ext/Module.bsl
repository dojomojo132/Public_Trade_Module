#Область УправлениеДокументов

Процедура СоздатьДокументЗаказТоваров(ДанныеЗаказа, Ид_Счета = 0, СписокКоментариев = "") Экспорт 
	
	Если НЕ ПроверкиДанных.ВалидацияВходныхДанных(ДанныеЗаказа.ТаблицаЗаказа, "ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли;       
	
	Заведение = ДанныеЗаказа.Заведение;
	Цех = ДанныеЗаказа.Цех;
	ДатаЗаказа = ДанныеЗаказа.ДатаЗаказа; 
	ТаблицаЗаказа = ДанныеЗаказа.ТаблицаЗаказа; 
	
	Док = Документы.ЗаказТовара.СоздатьДокумент();
	Док.Дата = НачалоДня(ДатаЗаказа);
	Док.Заведение = Заведение;
	Док.Цех = Цех;
	Док.Ид_Счета = Ид_Счета;
	
	Для Каждого Товар Из ТаблицаЗаказа Цикл
		Если Товар.Количество=0 Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Док.СписокЗаказ.Добавить();
		НоваяСтрока.Номенклатура = Товар.Номенклатура;
		НоваяСтрока.Количество = Товар.Количество;
		Если НЕ Цех = Справочники.ЗоныПриготовления.Общая Тогда
			НоваяСтрока.Коментарий = Товар.Коментарий;
		Иначе
			МассивКоментариев = СписокКоментариев.Получить(Товар.Номенклатура);
			НоваяСтрока.Коментарий = ?(НЕ МассивКоментариев = Неопределено,УниверсальныеОбработчикиДанных.МассивВСтроку(МассивКоментариев),"");
		КонецЕсли;
		
	КонецЦикла;
	
	Попытка
		Док.Записать(); 
		ТекстОповещения = "Документ заказ для " + Цех + " на " + Заведение + " за " + Формат(ДатаЗаказа, "ДФ=dd.MM.yy") + " успешно создан!";
		ВВП.ОтправитьОповещениеВЧат(Константы.ИдВладельца.Получить(), ТекстОповещения);
	Исключение
		ТекстОповещения = "Документ заказ для " + Цех + " на " + Заведение + " за " + Формат(ДатаЗаказа, "ДФ=dd.MM.yy")
					+ " не удалось создать по причине " + ОписаниеОшибки(); 
		ВВП.ОтправитьОповещениеВЧат(Константы.ИдВладельца.Получить(), ТекстОповещения);
	КонецПопытки;
	
КонецПроцедуры

Процедура СоздатьДокументСумированиеЗаказов(Заведение, ДатаЗаказов) Экспорт 
	
	Заказ = Новый ТаблицаЗначений;
	Заказ.Колонки.Добавить("Номенклатура");
	Заказ.Колонки.Добавить("Количество");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказТовараСписокЗаказ.Номенклатура КАК Номенклатура,
		|	ЗаказТовараСписокЗаказ.Количество КАК Количество,
		|	ЗаказТовараСписокЗаказ.Ссылка.Дата КАК Дата,
		|	ЗаказТовараСписокЗаказ.Коментарий КАК Коментарий
		|ИЗ
		|	Документ.ЗаказТовара.СписокЗаказ КАК ЗаказТовараСписокЗаказ
		|ГДЕ
		|	ЗаказТовараСписокЗаказ.Ссылка.Заведение = &Заведение
		|	И ЗаказТовараСписокЗаказ.Ссылка.Цех <> &Цех
		|	И ЗаказТовараСписокЗаказ.Ссылка.Дата = &Дата";
	
	Запрос.УстановитьПараметр("Заведение", Заведение);
	Запрос.УстановитьПараметр("Цех", Справочники.ЗоныПриготовления.Общая);  
	Запрос.УстановитьПараметр("Дата", НачалоДня(ДатаЗаказов));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	СоответствиеТоварКомментарии = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Заказ.Добавить();
		НоваяСтрока.Номенклатура = Выборка.Номенклатура;
		НоваяСтрока.Количество = Выборка.Количество; 
		
		Товар = Выборка.Номенклатура;  // Или Выборка.Товар, если это ссылка
	    Комментарий = Выборка.Коментарий;
	    Если ЗначениеЗаполнено(Комментарий) Тогда
		    // Получаем существующий массив или создаем новый
		    МассивКомментариев = СоответствиеТоварКомментарии.Получить(Товар);
		    Если МассивКомментариев = Неопределено Тогда
		        МассивКомментариев = Новый Массив;
		        СоответствиеТоварКомментарии.Вставить(Товар, МассивКомментариев);
		    КонецЕсли;
		    
		    // Добавляем комментарий в массив
		    МассивКомментариев.Добавить(Комментарий);
		КонецЕсли;
	КонецЦикла;
	
	Если Заказ.Количество() = 0 Тогда
		ТекстОповещения = "Товаров в заказе " + Заведение + " за " + Формат(ДатаЗаказов, "ДФ=dd.MM.yy") + " не было обнаружено!";
		ВВП.ОтправитьОповещениеВЧат(0, ТекстОповещения);
		
		Возврат;
	КонецЕсли;
	
	Заказ.Свернуть("Номенклатура", "Количество");
	ДанныеЗаказа = Новый Структура; 
	ДанныеЗаказа.Вставить("Цех", Справочники.ЗоныПриготовления.Общая);
	ДанныеЗаказа.Вставить("ДатаЗаказа", ДатаЗаказов);
	ДанныеЗаказа.Вставить("Заведение", Заведение);
	ДанныеЗаказа.Вставить("ТаблицаЗаказа", Заказ);

	СоздатьДокументЗаказТоваров(ДанныеЗаказа, 0, СоответствиеТоварКомментарии); 
	
КонецПроцедуры
#КонецОбласти       