Процедура ЗарегистрироватьПодписчика(Знач Событие, Знач Обработчик, Знач ИдентификаторПодписчика, Знач КодОбработчика) Экспорт
    Если Событие.Пустая() Тогда
        ЗаписьЖурналаРегистрации("EventBus.Ошибка", 
            УровеньЖурналаРегистрации.Ошибка, , , 
            "Попытка регистрации с пустым событием: " + ИдентификаторПодписчика);
        Возврат;
    КонецЕсли;
    
    Если ПустаяСтрока(КодОбработчика) Тогда
        ЗаписьЖурналаРегистрации("EventBus.Ошибка", 
            УровеньЖурналаРегистрации.Ошибка, , , 
            "Попытка регистрации с пустым кодом обработчика: " + ИдентификаторПодписчика);
        Возврат;
    КонецЕсли;
    
    МенеджерЗаписи = РегистрыСведений.ПодписчикиСобытий.СоздатьМенеджерЗаписи();
    МенеджерЗаписи.Событие = Событие;
    МенеджерЗаписи.ИдентификаторПодписчика = ИдентификаторПодписчика;
    МенеджерЗаписи.КодОбработчика = КодОбработчика;
    МенеджерЗаписи.Прочитать();
    
    Если НЕ МенеджерЗаписи.Выбран() Тогда
        МенеджерЗаписи.Событие = Событие;
        МенеджерЗаписи.ИдентификаторПодписчика = ИдентификаторПодписчика;
        МенеджерЗаписи.КодОбработчика = КодОбработчика;
        МенеджерЗаписи.Обработчик = Обработчик;
        МенеджерЗаписи.Записать();
        ЗаписьЖурналаРегистрации("EventBus.Регистрация", 
            УровеньЖурналаРегистрации.Информация, , , 
            "Подписчик зарегистрирован: " + ИдентификаторПодписчика + ", Событие: " + Событие.Наименование + ", Код: " + КодОбработчика);
    Иначе
        ЗаписьЖурналаРегистрации("EventBus.Регистрация", 
            УровеньЖурналаРегистрации.Информация, , , 
            "Подписчик уже существует: " + ИдентификаторПодписчика + ", Код: " + КодОбработчика);
    КонецЕсли;
КонецПроцедуры

Процедура ОпубликоватьСобытие(Знач Событие, Знач Параметры = Неопределено) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |   ПодписчикиСобытий.Обработчик КАК Обработчик
    |ИЗ
    |   РегистрСведений.ПодписчикиСобытий КАК ПодписчикиСобытий
    |ГДЕ
    |   ПодписчикиСобытий.Событие = &Событие";
    
    Запрос.УстановитьПараметр("Событие", Событие);
    
    Выборка = Запрос.Выполнить().Выбрать();
    
    Если Выборка.Количество() = 0 Тогда
        ЗаписьЖурналаРегистрации("EventBus.Ошибка", 
            УровеньЖурналаРегистрации.Предупреждение, , , 
            "Нет подписчиков для события: " + Событие.Наименование);
    КонецЕсли;
    
    Пока Выборка.Следующий() Цикл
        ЗаписьЖурналаРегистрации("EventBus.ВызовОбработчика", 
            УровеньЖурналаРегистрации.Информация, , , 
            "Вызов обработчика: " + Выборка.Обработчик);
        Попытка
            СтрокаВызова = Выборка.Обработчик + "(Событие, Параметры)";
            Выполнить(СтрокаВызова);
        Исключение
            ЗаписьЖурналаРегистрации("EventBus.ОшибкаОбработки", 
                УровеньЖурналаРегистрации.Ошибка, , , 
                "Ошибка вызова обработчика " + Выборка.Обработчик + ": " + ОписаниеОшибки());
        КонецПопытки;
    КонецЦикла;
КонецПроцедуры